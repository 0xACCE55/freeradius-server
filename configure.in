dnl #############################################################
dnl #
dnl #  For information about autoconf, see:
dnl #
dnl #  http://www.gnu.org/software/autoconf/
dnl #  
dnl #  The recommended order is:
dnl #
dnl #  AC_INIT(file)
dnl #  0. checks for compiler, libtool, and command line options
dnl #  1. checks for programs
dnl #  2. checks for libraries
dnl #  3. checks for header files
dnl #  4. checks for typedefs
dnl #  5. checks for structures and functions
dnl #  6. checks for compiler characteristics
dnl #  7. checks for library functions
dnl #  8. checks for system services
dnl #  AC_OUTPUT([file...])
dnl #
dnl #############################################################

AC_INIT(src/main/radiusd.c)
AC_CONFIG_HEADER(src/include/autoconf.h)
AC_REVISION($Revision$)dnl

dnl # The version of the software, 0.4
RADIUSD_VERSION=0.4

dnl #############################################################
dnl #
dnl #  0. Checks for compiler, libtool, and command line options.
dnl #
dnl #############################################################

dnl Check for GNU cc
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_CC_SUNPRO
AC_PROG_RANLIB

PACKAGE=freeradius

dnl Find GNU Make.
AC_CHECK_PROG(GMAKE, gmake, yes, no)
if test $GMAKE = no; then
  AC_PATH_PROG(MAKE, make, /usr/local/bin/make)
else
  AC_PATH_PROG(MAKE, gmake, /usr/local/gnu/bin/make)
fi
makever=`$ac_cv_path_MAKE --version 2>&1 | grep "GNU Make"`
if test -z "$makever"; then
  AC_MSG_ERROR(GNU Make is not installed.  Please download and install it
		from ftp://prep.ai.mit.edu/pub/gnu/make/ before continuing.)
fi

dnl libltdl is installable
AC_LIBLTDL_INSTALLABLE

dnl ensure that we're looking for dlopen
AC_LIBTOOL_DLOPEN

dnl Figure out how to build shared libraries
AC_PROG_LIBTOOL

dnl Put this in later, when all distributed modules use autoconf.
dnl AC_ARG_WITH(disablemodulefoo,
dnl [  --without-rlm_foo       Disables module compilation.  Module list:]
dnl esyscmd([find src/modules -type d -name rlm_\* -print |\
dnl 	sed -e 's%src/modules/.*/% (sub)- %; s%.*/%- %' |\
dnl 	awk '{print "                            "$0}']))

AC_ARG_ENABLE(strict-dependencies,
[  --enable-strict-dependencies  Fail configure on lack of module dependancy.])

AC_ARG_WITH(fooincludedir,
[  --with-FOO-include-dir=DIR   Dir for includes for FOO, module dependancy.])

AC_ARG_WITH(foolibdir,
[  --with-FOO-lib-dir=DIR  Dir for libraries for FOO, module dependancy.])

dnl extra argument: --with-logdir
logdir='${localstatedir}/log/radius'
AC_MSG_CHECKING(logdir)
AC_ARG_WITH(logdir,
[  --with-logdir=DIR       Directory for logfiles [LOCALSTATEDIR/log/radius] ],
[ case "$withval" in
    no)
	AC_MSG_ERROR(Need logdir)
	;;
    yes)
	;;
    *)
	logdir="$withval"
	;;
  esac ]
)
AC_SUBST(logdir)
AC_MSG_RESULT($logdir)

dnl extra argument: --with-radacctdir
radacctdir='${logdir}/radacct'
AC_MSG_CHECKING(radacctdir)
AC_ARG_WITH(radacctdir,
[  --with-radacctdir=PATH  Directory for detail files [LOGDIR/radacct] ],
[ case "$withval" in
    no)
	AC_MSG_ERROR(Need radacctdir)
	;;
    yes)
	;;
    *)
	radacctdir="$withval"
	;;
  esac ]
)
AC_SUBST(radacctdir)
AC_MSG_RESULT($radacctdir)

dnl extra argument: --with-raddbdir
raddbdir='${sysconfdir}/raddb'
AC_MSG_CHECKING(raddbdir)
AC_ARG_WITH(raddbdir,
[  --with-raddbdir=DIR     Directory for config files [SYSCONFDIR/raddb] ],
[ case "$withval" in
    no)
	AC_MSG_ERROR(Need raddbdir)
	;;
    yes)
	;;
    *)
	raddbdir="$withval"
	;;
  esac ]
)
AC_SUBST(raddbdir)
AC_MSG_RESULT($raddbdir)

dnl extra argument: --with-dict-nocase
WITH_DICT_NOCASE=yes
AC_ARG_WITH(dict-nocase,
[  --with-dict-nocase      Make dictionary case-independant (default=yes)],
[  case "$withval" in
    yes)
	;;
    *)
	WITH_DICT_NOCASE=""
  esac ]
)
if test "X$WITH_DICT_NOCASE" = "Xyes"; then
  AC_DEFINE(WITH_DICT_NOCASE)
fi

dnl extra argument: --with-ascend-binary
ASCEND_BINARY=yes
AC_ARG_WITH(ascend-binary,
[  --with-ascend-binary    Include support for Ascend binary filter attributes (default=yes)],
[ case "$withval" in
    yes)
	;;
    *)
	ASCEND_BINARY=""
  esac ]
)
if test "X$ASCEND_BINARY" = "Xyes"; then
  AC_DEFINE(ASCEND_BINARY)
fi

dnl extra argument: --with-threads
WITH_THREADS=yes
WITH_THREAD_POOL=yes
AC_ARG_WITH(threads,
[  --with-threads          Use threads, if available.  (default=yes) ],
[ case "$withval" in
    yes)
	;;
    *)
	WITH_THREADS=""
        WITH_THREAD_POOL=""
  esac ]
)

dnl extra argument: --with-snmp
WITH_SNMP=yes
AC_ARG_WITH(snmp,
[  --with-snmp             Compile in SNMP support. (default=yes)],
[ case "$withval" in
    yes)
        ;;
    *)
	WITH_SNMP=no
  esac ]
)

AC_ARG_ENABLE(ltdl-install,
[  --disable-ltdl-install  do not install libltdl])
if test x"${enable_ltdl_install+set}" != xset; then
  enable_ltdl_install=yes
  ac_configure_args="$ac_configure_args --enable-ltdl-install"
fi
AC_CONFIG_SUBDIRS(libltdl)

dnl #
dnl #  Allow the user to specify a list of modules to be linked
dnl #  statically to the server.
dnl #
STATIC_MODULES=
AC_ARG_WITH(static_modules,
[  --with-static-modules=QUOTED-MODULE-LIST],[
  for i in $withval; do
    STATIC_MODULES="$STATIC_MODULES -dlpreopen ../modules/rlm_$i/rlm_$i.la"
  done
])

dnl #
dnl #  Enable developer C compiler warnings
dnl #
AC_ARG_ENABLE(developer,
[  --enable-developer      Turns on flags of interest to developers.],
[ case "$withval" in
    *)
	developer=no
	;;
    yes)
	developer=yes
  esac ]
)

if test "x$developer" != "xno" -a -d $srcdir/CVS; then
  dnl turn on the developer flag when taken from a CVS checkout (not a release)
  developer="yes"
fi

dnl #
dnl #  These next two arguments don't actually do anything.  They're
dnl #  place holders so that the top-level configure script can tell
dnl #  the user how to configure lower-level modules
dnl #

dnl #
dnl # extra argument: --with-rlm-FOO-lib-dir
dnl #
AC_ARG_WITH(rlm-FOO-lib-dir,
[  --with-rlm-FOO-lib-dir=DIR       Directory to look for library files used by module FOO],
[ case "$withval" in
    *)
	;;
  esac ]
)

dnl #
dnl # extra argument: --with-rlm-FOO-include-dir
dnl #
AC_ARG_WITH(rlm-FOO-include-dir,
[  --with-rlm-FOO-include-dir=DIR   Directory to look for include files used by module FOO],
[ case "$withval" in
    *)
	;;
  esac ]
)

dnl See what include-style is used by the make program.
dnl AC_MSG_CHECKING(include style for make)
dnl echo "include /dev/null" > testmake.$$
dnl echo "all:" >> testmake.$$
dnl make -f testmake.$$ >/dev/null 2>&1
dnl if test $? = 0
dnl then
dnl 	INCLUDE=include
dnl 	IQUOTE=
dnl else
dnl 	INCLUDE=.include
dnl 	IQUOTE='"'
dnl fi
dnl rm -f testmake.$$
dnl AC_MSG_RESULT(" $INCLUDE")
dnl AC_SUBST(INCLUDE)
dnl AC_SUBST(IQUOTE)

dnl #############################################################
dnl #
dnl #  1. Checks for programs
dnl #
dnl #############################################################

AC_PATH_PROG(PERL, perl, /usr/local/bin/perl)
if test ! -x $ac_cv_path_PERL; then
  AC_MSG_ERROR(perl not found - fatal)
fi
AC_PATH_PROG(SNMPGET, snmpget, /usr/local/bin/snmpget)
AC_PATH_PROG(SNMPWALK, snmpwalk, /usr/local/bin/snmpwalk)
AC_PATH_PROG(RUSERS, rusers, /usr/bin/rusers)

dnl FIXME This is truly gross.
missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)

AC_PATH_PROG(LOCATE,locate)
AC_PATH_PROG(DIRNAME,dirname)
AC_PATH_PROG(GREP,grep)

dnl #
dnl # check for AIX, to allow us to use some BSD functions
dnl # must be before macros that call the compiler.
dnl #
AC_AIX

dnl #############################################################
dnl #
dnl #  2. Checks for libraries
dnl #
dnl #############################################################

dnl If using pthreads, check for -lpthread (posix) or -lc_r (*BSD)
PTHREADLIB=
if test "$WITH_THREADS" = "yes"; then 
  if test $ac_cv_prog_suncc = "yes"; then
    CFLAGS="$CFLAGS -mt"
  fi

  AC_CHECK_HEADERS(pthread.h)

  if test "X$ac_cv_header_pthread_h" = "X"; then
    AC_MSG_ERROR("You are trying to use threads without a POSIX <pthread.h>")
  fi

dnl # 
dnl # pthread stuff is usually in -lpthread
dnl # 
  AC_CHECK_LIB(pthread, pthread_join,
    PTHREADLIB="-lpthread"
  )

dnl # HP/UX requires linking with librt, too, to get the sem_* symbols.  :\

  AC_SEARCH_LIBS(sem_init, pthread rt,
	[ 
	dnl    Er, this usage of $ac_cv_search_sem_init may be unfounded 
	dnl    chumminess with the autoconf internals.  -chad
	PTHREADLIB="$PTHREADLIB $ac_cv_search_sem_init"
	],
	[],
	"-lpthread"
  )

dnl # 
dnl # Except for *BSD, where it's in -lc_r,
dnl # and where we've got to add extra CFLAGS.
dnl # 
  if test "X$PTHREADLIB" = "X"; then
    SAVED_CFLAGS=$CFLAGS
    CFLAGS="$CFLAGS -pthread -D_THREAD_SAFE"
    AC_CHECK_LIB(c_r, pthread_create,
      PTHREADLIB="-lc_r"
    )
  fi

dnl # Set libs, etc, if we have them.
  if test "x$PTHREADLIB" != "x"; then
    LIBS="$LIBS $PTHREADLIB"
    CFLAGS="$CFLAGS -D_REENTRANT"
  fi
fi

dnl # 
dnl # If we have NO pthread libraries, remove any knowledge of threads.
dnl # 
if test "x$PTHREADLIB" = "x"; then
  CFLAGS=$SAVED_CFLAGS
  ac_cv_header_pthread_h="no"
  WITH_THREADS=no
  WITH_THREAD_POOL=no
fi

dnl Check if we need -lsocket
AC_CHECK_LIB(socket, getsockname)

dnl Check for -lresolv
dnl This library may be needed later.
AC_CHECK_LIB(resolv, inet_aton)

dnl Check if we need -lnsl. Usually if we want to
dnl link against -lsocket we need to include -lnsl as well.
AC_CHECK_LIB(nsl, inet_ntoa)

dnl #############################################################
dnl #
dnl #  3. Checks for header files
dnl #
dnl #############################################################

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_SYS_WAIT

AC_CHECK_HEADERS( \
	unistd.h \
	crypt.h \
	errno.h \
	resource.h \
	getopt.h \
	malloc.h \
	utmp.h \
	utmpx.h \
	signal.h \
	sys/select.h \
	syslog.h \
	inttypes.h \
	stdint.h \
	stdio.h \
	netdb.h \
	semaphore.h \
	arpa/inet.h \
	netinet/in.h \
	sys/types.h \
	sys/socket.h \
	sys/time.h \
	sys/security.h \
	prot.h \
	sia.h \
	siad.h
)

REGEX=no
AC_CHECK_HEADER(regex.h, AC_DEFINE(HAVE_REGEX_H))
if test "x$ac_cv_header_regex_h" = "xyes"; then
  REGEX_EXTENDED=no
  REGEX=yes
  AC_EGREP_CPP(yes,
    [#include <regex.h>
     #ifdef REG_EXTENDED
       yes
     #endif
     ], [AC_DEFINE(HAVE_REG_EXTENDED) REGEX_EXTENDED=yes])
fi
AC_SUBST(REGEX)
AC_SUBST(REGEX_EXTENDED)

dnl #
dnl #  other checks which require headers
dnl #
if test "x$ac_cv_header_sys_security_h" = "xyes" && test "x$ac_cv_header_prot_h" = "xyes"
then
  AC_DEFINE(OSFC2)
fi

if test "x$ac_cv_header_sia_h" = "xyes" && test "x$ac_cv_header_siad_h" = "xyes"
then
  AC_DEFINE(OSFSIA)
fi


dnl #############################################################
dnl #
dnl #  4. Checks for typedefs
dnl #
dnl #############################################################

dnl #
dnl # Ensure that these are defined
dnl #
AC_TYPE_OFF_T 
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T

dnl check for socklen_t
AC_CHECK_TYPE_INCLUDE([
#ifdef HAVE_SYS_TYPES_H
#include <sys/types.h>
#endif
#ifdef HAVE_SYS_SOCKET_H
#include <sys/socket.h>
#endif
],socklen_t, int)

dnl check for uint8_t
AC_CHECK_TYPE_INCLUDE([
#ifdef HAVE_INTTYPES_H
#include <inttypes.h>
#endif
#ifdef HAVE_STDINT_H
#include <stdint.h>
#endif
],uint8_t, unsigned char)

dnl check for uint32_t
AC_CHECK_TYPE_INCLUDE([
#ifdef HAVE_INTTYPES_H
#include <inttypes.h>
#endif
#ifdef HAVE_STDINT_H
#include <stdint.h>
#endif
],uint32_t, unsigned int)

dnl #############################################################
dnl #
dnl #  5. Checks for structures and functions
dnl #
dnl #############################################################
AC_CHECK_FUNCS( \
	getopt_long \
	lockf \
	strsignal \
	sigaction \
	sigprocmask \
	snprintf \
	vsnprintf \
	setsid \
	strncasecmp \
	strcasecmp \
	strsep \
	inet_aton \
	gethostname \
	setlinebuf \
	setvbuf \
	sem_init \
	getusershell
)
RADIUSD_NEED_DECLARATIONS( \
	crypt \
	strncasecmp \
	strcasecmp \
	inet_aton \
	gethostname \
	setlinebuf \
	getusershell \
	endusershell
)

AC_TYPE_SIGNAL

dnl # check if we have utmpx.h
dnl # if so, check if struct utmpx has entry ut_xtime
dnl # if not, set it to define ut_xtime == ut_tv.tv_sec
if test "x$ac_cv_header_utmpx_h" = "xyes"
then
 AC_CHECK_STRUCT_HAS_MEMBER([#include <utmpx.h>], [struct utmpx], ut_xtime)
 if test "x$ac_cv_type_struct_utmpx_ut_xtime" = "x"
 then
   AC_DEFINE(ut_xtime,ut_tv.tv_sec)
 fi
fi

dnl #############################################################
dnl #
dnl #  6. Checks for compiler characteristics
dnl #
dnl #############################################################

dnl #
dnl # Ensure that these are defined
dnl #
AC_C_CONST 

dnl #
dnl # See if this is OS/2
dnl #
AC_MSG_CHECKING(type of OS)
OS=`uname -s`
AC_MSG_RESULT($OS)
if test "$OS" = "OS/2"; then
	LIBPREFIX=
else
	LIBPREFIX=lib
fi
AC_SUBST(LIBPREFIX)

dnl #
dnl # Set Default CFLAGS
dnl #
if test "x$GCC" = "xyes"; then
    CFLAGS="$CFLAGS -Wall -D_GNU_SOURCE"
fi

AC_MSG_CHECKING(for developer gcc flags)
if test "x$developer" = "xyes" -a "x$GCC" = "xyes"; then
  devflags="-g -Wshadow -Wpointer-arith -Wcast-qual -Wcast-align -Wwrite-strings -Wstrict-prototypes -Wmissing-prototypes -Wmissing-declarations -Wnested-externs"
  CFLAGS="$CFLAGS $devflags"
  INSTALLSTRIP=""
  AC_MSG_RESULT(yes.  Using $devflags)
else
  devflags=""
  CFLAGS="$CFLAGS -DNDEBUG"
  INSTALLSTRIP="-s"
  AC_MSG_RESULT(no.)
fi

dnl #############################################################
dnl #
dnl #  7. Checks for library functions
dnl #
dnl #############################################################

dnl Check for libcrypt
dnl We use crypt(3) which may be in libc, or in libcrypt (eg FreeBSD)
AC_CHECK_LIB(crypt, crypt,
  CRYPTLIB="-lcrypt"
)
AC_SUBST(CRYPTLIB)
if test "$CRYPTLIB" != ""; then
  AC_DEFINE(HAVE_CRYPT)
else
  AC_CHECK_FUNC(crypt, AC_DEFINE(HAVE_CRYPT))
fi

if test "x$WITH_SNMP" = "xyes"; then
  SNMP_CHECKS
fi

dnl #
dnl #  Check for sem_init all by itself
dnl #
if test "X$ac_cv_func_sem_init" = "Xno"; then

dnl #
dnl #  If not found, sem_init is in -lposix4 on Solaris
dnl #
 AC_CHECK_LIB(posix4, sem_init)

  if test "X$ac_cv_lib_posix4_sem_init" = "X"; then
    if test "$WITH_THREAD_POOL" = "yes"; then
      AC_MSG_WARN(You do not have posix semaphores.  Disabling thread pools)
    fi
    WITH_THREAD_POOL=no
  fi
fi

ODBC_CHECKS

dnl #############################################################
dnl #
dnl #  8. Checks for system services
dnl #
dnl #############################################################

dnl #
dnl # Figure out where libtool is located,
dnl #
LIBTOOL="`pwd`/libtool"
AC_SUBST(LIBTOOL)
AC_SUBST(LIBLTDL)
AC_SUBST(INCLTDL)
top_builddir=`pwd`
AC_SUBST(top_builddir)

dnl import libtool stuff

dnl #
dnl #  Checks to ensure that the thread pool code can build.
dnl #
if test "$WITH_THREAD_POOL" = "yes"; then
  dnl #
  dnl # If using thread pools, check that threading is enabled
  dnl #
  if test "$WITH_THREADS" != "yes"; then
    AC_MSG_ERROR(You must have threads enabled for thread pools to work.)
  fi

  dnl #
  dnl # If using thread pools, check that we have thread libraries
  dnl #
  if test "X$PTHREADLIB" = "X"; then
    AC_MSG_ERROR(You must have threading libraries for thread pools to work.)
  fi

dnl #
dnl #  Finally, after all of the checks above, we see if
dnl #  we can REALLY use the thread pools.
dnl #
  if test "$WITH_THREAD_POOL" = "yes"; then
    AC_DEFINE(WITH_THREAD_POOL,1)
  fi
fi

dnl #############################################################
dnl #
dnl #  Configure in any module directories.
dnl #
dnl #############################################################

mysubdirs=
for foo in `find src/modules -name configure -print`; do
  bar=`echo $foo | sed 's%/configure$%%g'`
  mysubdirs="$mysubdirs $bar"
done

dnl #
dnl #  Don't change the variable name here.  Autoconf goes bonkers
dnl #  if you do.
dnl #
AC_CONFIG_SUBDIRS($mysubdirs)

dnl #############################################################
dnl #
dnl #  And finally, output the results.
dnl #
dnl #############################################################

dnl #
dnl #  Substitute whatever libraries we found to be necessary
dnl #
AC_SUBST(LIBS)
AC_SUBST(INSTALLSTRIP)

USE_SHARED_LIBS=$enable_shared
AC_SUBST(USE_SHARED_LIBS)
USE_STATIC_LIBS=$enable_static
AC_SUBST(USE_STATIC_LIBS)
AC_SUBST(STATIC_MODULES)
AC_SUBST(RADIUSD_VERSION)
export CFLAGS LIBS

AC_OUTPUT(\
	./Make.inc \
	./src/include/build-radpaths-h \
	./src/main/checkrad.pl \
	./src/main/radlast \
	./src/main/radtest \
	./scripts/rc.radiusd \
	./scripts/radwatch \
	./scripts/check-radiusd-config \
	./scripts/radiusd.cron.daily \
	./scripts/radiusd.cron.monthly \
	./scripts/cryptpasswd \
	./raddb/radiusd.conf
)

AC_OUTPUT_COMMANDS([echo timestamp > src/include/stamp-h])
AC_OUTPUT_COMMANDS([(cd ./src/include && /bin/sh ./build-radpaths-h)])
AC_OUTPUT_COMMANDS([(cd ./src/main   && chmod +x checkrad.pl radlast radtest)])
AC_OUTPUT_COMMANDS([(cd ./scripts    && chmod +x rc.radiusd radwatch check-radiusd-config radiusd.cron.daily radiusd.cron.monthly cryptpasswd)])
AC_OUTPUT_COMMANDS([
cat >> src/include/autoconf.h <<EOF

#define HOSTINFO	"$host"
#define RADIUSD_VERSION "$RADIUSD_VERSION"
EOF
],host=$host)
